// Lexical

WhiteSpace:
	Space {Space}

Space:
	' '
	'\t'
	'\v'
	'\u000C'
	EndOfLine
	Comment
	
EndOfLine:
	'\r'
	'\n'
	'\r\n'
	EndOfFile

EndOfFile:
	physical end of file
	'\0'

Comment:
	'/*' characters '*/'
	'//' characters EndOfLine

Token:
	Identifier
	Keyword
	CharLiteral
	StringLiteral
	IntLiteral
	FloatLiteral
	'+'
	'+='
	'++'
	'-'
	'-='
	'--'
	'~'
	'~='
	'*'
	'*='
	'/'
	'/='
	'%'
	'%='
	'<'
	'<='
	'<<'
	'<<='
	'>'
	'>='
	'>>'
	'>>='
	'>>>'
	'>>>='
	'&'
	'&='
	'&&'
	'|'
	'|='
	'||'
	'^'
	'^='
	'='
	'=='
	'.'
	'..'
	'!'
	'!='
	'('
	')'
	'['
	']'
	'{'
	'}'
	':'
	','
	';'
	'#'
	EOF

Identifier:
	IdentifierStart {IdentifierChar}

IdentifierStart:
	_
	Letter

IdentifierChar:
	IdentifierStart
	DecimalDigit

Keyword:
	break
	case
	catch
	continue
	default
	do
	else
	false
	finally
	for
	foreach
	function
	if
	is
	local
	null
	return
	switch
	throw
	true
	try
	vararg
	while

StringLiteral:
	RegularString
	WysiwygString
	AltWysiwygString

RegularString:
	'"' {Character | EscapeSequence | EndOfLine} '"'

EscapeSequence:
	'\''
	'\"'
	'\\'
	'\a'
	'\b'
	'\f'
	'\n'
	'\r'
	'\t'
	'\v'
	'\x' HexDigit HexDigit
	'\u' HexDigit HexDigit HexDigit HexDigit
	'\U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit
	'\ ' DecimalDigit [DecimalDigit [DecimalDigit]]

WysiwygString:
	'@"' {Character | EndOfLine} '"'

AltWysiwygString:
	'`' {Character | EndOfLine} '`'

IntLiteral:
	Decimal
	Binary
	Octal
	Hexadecimal
	
Decimal:
	DecimalDigit {DecimalDigit | '_'}

DecimalDigit:
	0
	1
	2
	3
	4
	5
	6
	7
	8
	9

Binary:
	'0b' BinaryDigit {BinaryDigit | '_'}

BinaryDigit:
	0
	1

Octal:
	'0c' OctalDigit {OctalDigit | '_'}

OctalDigit:
	0
	1
	2
	3
	4
	5
	6
	7

Hexadecimal:
	'0x' HexDigit {HexDigit | '_'}

HexDigit:
	0
	1
	2
	3
	4
	5
	6
	7
	8
	9
	A
	a
	B
	b
	C
	c
	D
	d
	E
	e
	F
	f
	
FloatLiteral:
	[DecimalDigit {DecimalDigit | '_'}] '.' DecimalDigit {DecimalDigit | '_'} [Exponent]
	DecimalDigit {DecimalDigit | '_'} [Exponent]

Exponent:
	('e' | 'E')['+' | '-'] DecimalDigit {DecimalDigit | '_'}

// -----------------------------------------------------------------------------------------------------------------
// -----------------------------------------------------------------------------------------------------------------
// -----------------------------------------------------------------------------------------------------------------

// Syntax

Chunk:
	{Statement}

Statement:
	BlockStatement
	ExpressionStatement
    DeclarationStatement
    'if' '(' Expression ')' Statement ['else' Statement]
    'while' '(' Expression ')' Statement
    'do' Statement 'while' '(' Expression ')'
    'for' '(' [(BaseExpression | LocalVariableDeclaration)] ';' [Expression] ';' [BaseExpression] ')' Statement
    'foreach' '(' 'local' Identifier {',' 'local' Identifier} ';' Expression ')' Statement
    'switch' '(' Expression ')' '{' {CaseStatement} [DefaultStatement] '}'
    'case' Expression {',' Expression} ':' {Statement}
    'default' ':' {Statement}
    'continue' ';'
    'break' ';'
    'return' [Expression {',' Expression}] ';'
    'try' BlockStatement 'catch' '(' Identifier ')' BlockStatement ['finally' BlockStatement]

BlockStatement:
	'{' {Statement} '}'

ExpressionStatement:
	BaseExpression ';'

DeclarationStatement:
	LocalVariableDeclaration ';'
	FunctionDeclaration

LocalVariableDeclaration:
	'local' {Identifier} ['=' {Expression}]

FunctionDeclaration:
	'function' Identifier {'.' Identifier} [':' Identifier] Parameters BlockStatement
	'local' 'function' Identifier Parameters BlockStatement

Parameters:
	'(' [Identifier {',' Identifier}] ')'

BaseExpression:
	Assignment
	Expression
	
Assignment:
	AssignmentLHS {',' AssignmentLHS} '=' Expression {',' Expression}

AssignmentLHS:
	Identifier
	'.' Identifier
	PostfixExpression '[' Expression ']'
	PostfixExpression '.' Identifier

Expression:
	OrOrExpression
	OrOrExpression '+=' Expression
	OrOrExpression '-=' Expression
	OrOrExpression '~=' Expression
	OrOrExpression '*=' Expression
	OrOrExpression '/=' Expression
	OrOrExpression '%=' Expression
	OrOrExpression '<<=' Expression
	OrOrExpression '>>=' Expression
	OrOrExpression '>>>=' Expression
	OrOrExpression '|=' Expression
	OrOrExpression '^=' Expression
	OrOrExpression '&=' Expression

OrOrExpression:
	AndAndExpression
	OrOrExpression '||' AndAndExpression

AndAndExpression:
	OrExpression
	AndAndExpression '&&' OrExpression
	
OrExpression:
	XorExpression
	OrExpression '|' XorExpression

XorExpression:
	AndExpression
	XorExpression '^' AndExpression

AndExpression:
	EqualExpression
	AndExpression '&' EqualExpression

EqualExpression:
	RelExpression
	EqualExpression '==' RelExpression
	EqualExpression '!=' RelExpression
	EqualExpression 'is' RelExpression
	EqualExpression '!' 'is' RelExpression
	
RelExpression:
	ShiftExpression
	RelExpression '<' ShiftExpression
	RelExpression '<=' ShiftExpression
	RelExpression '>' ShiftExpression
	RelExpression '>=' ShiftExpression

ShiftExpression:
	ShiftExpression '<<' AddExpression
	ShiftExpression '>>' AddExpression
	ShiftExpression '>>>' AddExpression

AddExpression:
	MulExpression
	AddExpression '+' MulExpression
	AddExpression '-' MulExpression
	AddExpression '~' MulExpression

MulExpression:
	UnaryExpression
	MulExpression '*' UnaryExpression
	MulExpression '/' UnaryExpression
	MulExpression '%' UnaryExpression

UnaryExpression:
	PostfixExpression
	'++' UnaryExpression
	'--' UnaryExpression
	'-' UnaryExpression
	'!' UnaryExpression
	'~' UnaryExpression
	'#' UnaryExpression

PostfixExpression:
	PrimaryExpression
	PostfixExpression '[' Expression ']'
	PostfixExpression '.' Identifier
	PostfixExpression [':' Identifier] '(' [Expression {',' Expression}] ')'

PrimaryExpression:
	Identifier
	'.' Identifier
	'null'
	'true'
	'false'
	IntLiteral
	FloatLiteral
	CharLiteral
	StringLiteral
	'function' Parameters BlockStatement
	'(' Expression ')'
	TableCtor
	ArrayCtor
	
TableCtor:
	'{' [TableField {',' TableField}] '}'

TableField:
	Expression
	Identifier '=' Expression
	Expression ':' Expression

ArrayCtor:
	'[' [ArrayField {',' ArrayField}] ']'

ArrayField:
	Expression
	Expression ':' Expression