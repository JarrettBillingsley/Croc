/**
The console library provides basic console IO by wrapping the standard input, output, and error streams in
\link{stream.Stream} objects. This is a safe library. It also exports some functions into the global namespace for
convenience.
*/
module console

local TextReader, TextWriter, NativeStream = stream.TextReader, stream.TextWriter, stream.NativeStream

/**
These are the \link{stream.Stream} objects that wrap the standard input, output, and error streams. You can, however,
reassign these at will, which makes redirecting Croc's input and output trivial. For instance, if you wanted to
change the standard input stream to use a file instead of the console, you could simply do it like this:

\code
// Good idea to hold onto the old stream in case you want to set it back
local oldStream = console.stdin
console.stdin = TextReader(file.inFile("somefile.txt"))

// Now any use of stdin (including the global readln() function) will read from "somefile.txt" instead.
\endcode
*/
global stdin, stdout, stderr

stdin = TextReader(NativeStream(_consoletmp.stdin, "r"), "utf-8", "replace")
stdout = TextWriter(NativeStream(_consoletmp.stdout, "w"), "utf-8", "replace")
stderr = TextWriter(NativeStream(_consoletmp.stderr, "w"), "utf-8", "replace")

/**
This is a shortcut for calling \tt{stdout.write} with the given arguments.

Also mirrored in the global namespace so you can access it unqualified.

\see \link{stream.TextWriter.write}
*/
function write(vararg)
{
	stdout.write(vararg)
}

/**
This is a shortcut for calling \tt{stdout.writeln} with the given arguments.

Also mirrored in the global namespace so you can access it unqualified.

\see \link{stream.TextWriter.writeln}
*/
function writeln(vararg)
{
	stdout.writeln(vararg)
}

/**
This is a shortcut for calling \tt{stdout.writef} with the given arguments.

Also mirrored in the global namespace so you can access it unqualified.

\see \link{stream.TextWriter.writef}
*/
function writef(fmt: string, vararg)
{
	stdout.writef(fmt, vararg)
}

/**
This is a shortcut for calling \tt{stdout.writefln} with the given arguments.

Also mirrored in the global namespace so you can access it unqualified.

\see \link{stream.TextWriter.writefln}
*/
function writefln(fmt: string, vararg)
{
	stdout.writefln(fmt, vararg)
}

/**
This is a shortcut for calling \tt{stdin.readln}.

Also mirrored in the global namespace so you can access it unqualified.

\see \link{stream.TextReader.readln}
*/
function readln() =
	stdin.readln()

// Export write[f][ln] and readln to the global namespace
_G.write = write
_G.writeln = writeln
_G.writef = writef
_G.writefln = writefln
_G.readln = readln